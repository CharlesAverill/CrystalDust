const EARL = 1
const NOTES_KID = 2
const GBKID_1 = 4
const GBKID_2 = 5

const PSN = 0
const PAR = 1
const SLP = 2
const BRN = 3
const FRZ = 4
const EXIT = 5

mapscripts VioletCity_EarlsAcademy_MapScripts { }

script VioletCity_EarlsAcademy_EventScript_StatusInfoBoard {
	lockall
	msgbox("The blackboard lists POKéMON\n"
		   "status problems in battle.")
	goto(StatusBoard_Loop)
}

script(local) StatusBoard_Loop {
	message("Which topic do you want to read?")
	waitmessage
	multichoicegrid(8, 1, MULTI_STATUS_INFO, 3, FALSE)
	switch(var(VAR_RESULT))
	{
		case PSN:
			msgbox("If poisoned, a POKéMON steadily\n"
				   "loses HP.\p"
				   "Poison lingers after the battle,\n"
				   "and HP is lost as you walk.\p"
				   "To cure it, use an ANTIDOTE.")
			goto(StatusBoard_Loop)
		case PAR:
			msgbox("Paralysis reduces SPEED and may\n"
				   "prevent movement.\p"
				   "It remains after battle, so use a\n"
				   "PARLYZ HEAL.")
			goto(StatusBoard_Loop)
		case SLP:
			msgbox("If asleep, your POKéMON can't make\n"
				   "a move.\p"
				   "A sleeping POKéMON doesn't wake up\n"
				   "after battle, so wake it up with\l"
				   "an AWAKENING.")
			goto(StatusBoard_Loop)
		case BRN:
			msgbox("A burn steadily consumes HP.\n"
				   "It also reduces ATTACK power.\p"
				   "A burn lingers after battle.\n"
				   "Use a BURN HEAL as the cure.")
			goto(StatusBoard_Loop)
		case FRZ:
			msgbox("If your POKéMON is frozen, it can't\n"
				   "do a thing.\p"
				   "It remains frozen after battle.\n"
				   "Thaw it out with an ICE HEAL.")
			goto(StatusBoard_Loop)
		case EXIT:
		case MULTI_B_PRESSED:
			releaseall
			end
	}
}

script VioletCity_EarlsAcademy_EventScript_Notes {
	lockall
	msgbox("It's this kid's notebook…\p"
		   "Catch POKéMON using POKé BALLS.\n"
		   "Up to six can be in your party.\p"
		   "Keep reading?", MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES)
	{
		msgbox("Before throwing a POKé BALL, weaken\n"
			   "the target first.\p"
			   "A poisoned or burned POKéMON is\n"
			   "easier to catch.\p"
			   "Keep reading?", MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES)
		{
			msgbox("Some moves may cause confusion.\p"
				   "Confusion may make a POKéMON attack\n"
				   "itself.\p"
				   "Leaving battle clears up any\n"
				   "confusion.\p"
				   "Keep reading?", MSGBOX_YESNO)
			if (var(VAR_RESULT) == YES)
			{
				msgbox("People who catch and use POKéMON\n"
					   "in battle are POKéMON TRAINERS.\p"
					   "They are expected to visit POKéMON\n"
					   "GYMS and defeat other TRAINERS.\p"
					   "The next page is…\n"
					   "Blank!")
				applymovement(NOTES_KID, Common_Movement_WalkInPlaceFastestUp)
				waitmovement(0)
				textcolor(MSG_COLOR_BLUE)
				msgbox("BOY: Ehehe…\n"
					   "I haven't written any more…")
			}
		}
	}
	releaseall
}

// TODO: come back and clean up when local labels have been implemented
script VioletCity_EarlsAcademy_Earl {
	lockall
	applymovement(EARL, VioletCity_Movement_EarlSpin)
	waitmovement(0)
	faceplayer
    msgbox("EARL, I am!\n"
           "Wonderful are POKéMON, yes!\p"
           "Teach you I will to be a better\n"
           "TRAINER!\p"
           "What you want to know?\n"
           "Want to be a winner is you?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES)
    {
        msgbox("Good!\n"
               "Teach you, I will!\p"
               "In battle, POKéMON left of list\n"
               "jump out first!\p"
               "Change order in list, make battle\n"
               "easy, maybe!\p"
               "More from me you want to hear?", MSGBOX_YESNO)
        if (var(VAR_RESULT) == NO)
        {
            goto(Earl_End)
        }
    }

    msgbox("So, want to know how to raise\n"
           "POKéMON well?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES)
    {
        msgbox("Fine!\n"
               "Teach you, I will!\p"
               "If POKéMON come out in battle even\n"
               "briefly, some EXP. Points it gets.\p"
			   "At left of list put weak POKéMON.\n"
			   "Switch in battle quick!\p"
			   "This way, weak POKéMON strong\n"
			   "become!")
    }
	else
	{
		goto(Earl_End)
	}

    releaseall
}

script(local) Earl_End {
    msgbox("Oh! Smart student you are!\n"
           "Nothing more do I teach!")
    releaseall
}

movement VioletCity_EarlsAcademy_Movement_EarlSpin {
	face_down
	delay_1
	face_left
	delay_1
	face_up
	delay_1
	face_right
	delay_1
	face_down
	delay_1
	face_left
	delay_1
	face_up
	delay_1
	face_right
	delay_1
	face_down
	delay_1
	face_left
	delay_1
	face_up
	delay_1
	face_right
	delay_1
	face_down
}

script VioletCity_EarlsAcademy_EventScript_NoteTakingKid {
	msgbox("I'm taking notes of the teacher's\n"
		   "lecture.\p"
		   "I'd better copy the stuff on the\n"
		   "blackboard, too.", MSGBOX_NPC)
}

script VioletCity_EarlsAcademy_EventScript_TradingKid1 {
	lock
	faceplayer
	msgbox("I traded my best POKéMON to the\n"
	 	   "guy beside me.")
	closemessage
	applymovement(GBKID_1, Common_Movement_WalkInPlaceFastestDown)
	waitmovement(0)
	release
}

script VioletCity_EarlsAcademy_EventScript_TradingKid2 {
	lock
	faceplayer
	msgbox("Huh? The POKéMON I just got is\n"
	 	   "holding something!")
	closemessage
	applymovement(GBKID_2, Common_Movement_WalkInPlaceFastestDown)
	waitmovement(0)
	release
}

script VioletCity_EarlsAcademy_EventScript_HeldItemsKid {
	msgbox("A POKéMON holding a BERRY will heal\n"
		   "itself in battle…\p"
		   "Many other items can be held by\n"
		   "POKéMON…\p"
		   "It sure is tough taking notes…", MSGBOX_NPC)
}

raw `
VioletCity_EarlsAcademy_EventScript_204251:: @ 8204251
	lockall
	msgbox VioletCity_EarlsAcademy_Text_20446E, MSGBOX_DEFAULT
	giveitem_std ITEM_HM03
	setflag FLAG_RECEIVED_HM03
	msgbox VioletCity_EarlsAcademy_Text_20461A, MSGBOX_DEFAULT
	setvar VAR_VIOLET_CITY_STATE, 5
	releaseall
	end

VioletCity_EarlsAcademy_EventScript_204278:: @ 8204278
	lock
	faceplayer
	goto_if_set FLAG_DEFEATED_WALLY_VICTORY_ROAD, VioletCity_EarlsAcademy_EventScript_2042AC
	goto_if_set FLAG_RECEIVED_HM03, VioletCity_EarlsAcademy_EventScript_2042A2
	goto_if_set FLAG_THANKED_FOR_PLAYING_WITH_WALLY, VioletCity_EarlsAcademy_EventScript_2042B6
	msgbox VioletCity_EarlsAcademy_Text_2042DF, MSGBOX_DEFAULT
	setflag FLAG_THANKED_FOR_PLAYING_WITH_WALLY
	release
	end

VioletCity_EarlsAcademy_EventScript_2042A2:: @ 82042A2
	msgbox VioletCity_EarlsAcademy_Text_204661, MSGBOX_DEFAULT
	release
	end

VioletCity_EarlsAcademy_EventScript_2042AC:: @ 82042AC
	msgbox VioletCity_EarlsAcademy_Text_204698, MSGBOX_DEFAULT
	release
	end

VioletCity_EarlsAcademy_EventScript_2042B6:: @ 82042B6
	msgbox VioletCity_EarlsAcademy_Text_20444D, MSGBOX_DEFAULT
	release
	end

VioletCity_EarlsAcademy_EventScript_2042C0:: @ 82042C0
	lock
	faceplayer
	goto_if_set FLAG_RECEIVED_HM03, VioletCity_EarlsAcademy_EventScript_2042D5
	msgbox VioletCity_EarlsAcademy_Text_204732, MSGBOX_DEFAULT
	release
	end

VioletCity_EarlsAcademy_EventScript_2042D5:: @ 82042D5
	msgbox VioletCity_EarlsAcademy_Text_2047A7, MSGBOX_DEFAULT
	release
	end

VioletCity_EarlsAcademy_Text_2042DF: @ 82042DF
	.string "You're…\n"
	.string "Ah, you must be {PLAYER}{KUN}, right?\p"
	.string "Thank you for playing with WALLY a\n"
	.string "little while ago.\p"
	.string "He's been frail and sickly ever\n"
	.string "since he was a baby.\p"
	.string "We've sent him to stay with my relatives\n"
	.string "in VERDANTURF TOWN for a while.\p"
	.string "The air is a lot cleaner there\n"
	.string "than it is here.\p"
	.string "What's that? Where's WALLY?\n"
	.string "He's already left, our WALLY.\p"
	.string "I wonder where he could have\n"
	.string "gotten by now?$"

VioletCity_EarlsAcademy_Text_20444D: @ 820444D
	.string "I wonder how our WALLY is doing?$"

VioletCity_EarlsAcademy_Text_20446E: @ 820446E
	.string "{PLAYER}{KUN}! Please excuse us for\n"
	.string "dragging you here this way.\p"
	.string "But our WALLY's become very healthy\n"
	.string "since he went to VERDANTURF TOWN.\p"
	.string "We owe it all to you!\p"
	.string "When WALLY left town, you helped\n"
	.string "him catch a POKéMON, right?\p"
	.string "I think that made WALLY really\n"
	.string "happy.\p"
	.string "Actually, not just WALLY.\n"
	.string "It made me, his father, happy too.\p"
	.string "Happy that he's gained such a great\n"
	.string "friend as you.\p"
	.string "This isn't a bribe or anything, but\n"
	.string "I'd really like you to have this.$"

VioletCity_EarlsAcademy_Text_20461A: @ 820461A
	.string "If your POKéMON can SURF, you'll be\n"
	.string "able to go to all sorts of places.$"

VioletCity_EarlsAcademy_Text_204661: @ 8204661
	.string "WALLY's coming home soon.\n"
	.string "I'm looking forward to that.$"

VioletCity_EarlsAcademy_Text_204698: @ 8204698
	.string "Oh? You met WALLY in\n"
	.string "EVER GRANDE CITY?\p"
	.string "Oh, {PLAYER}{KUN}, don't be silly.\p"
	.string "He may have gotten healthy, but he\n"
	.string "can't go somewhere far away like\l"
	.string "that all by himself.$"

VioletCity_EarlsAcademy_Text_204732: @ 8204732
	.string "WALLY was really happy when he told\n"
	.string "us that he caught a POKéMON.\p"
	.string "It's been ages since I've seen him\n"
	.string "smile like that.$"

VioletCity_EarlsAcademy_Text_2047A7: @ 82047A7
	.string "I want you to keep this a secret\n"
	.string "from my husband…\p"
	.string "But our WALLY left VERDANTURF TOWN\n"
	.string "without telling anyone.\p"
	.string "You know, WALLY is frail, but\n"
	.string "he's surprisingly strong-willed.\p"
	.string "I'm sure that he'll come back safe\n"
	.string "and sound one day!$"
`