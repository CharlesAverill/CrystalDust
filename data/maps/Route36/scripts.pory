const SUDOWOODO = 3

mapscripts Route36_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		gettime
		if (var(VAR_0x8003) == DAY_THURSDAY)
		{
			clearflag(FLAG_HIDE_ROUTE_36_ARTHUR)
		}
		else
		{
			setflag(FLAG_HIDE_ROUTE_36_ARTHUR)
		}
	}
}

script Route36_EventScript_BlockedGirl {
	lock
	faceplayer
    if (!flag(FLAG_DEFEATED_SUDOWOODO))
    {
        msgbox("An odd tree is blocking the way\n"
               "to GOLDENROD CITY.\p"
               "I wanted to go see the huge\n"
               "POKéMON CENTER they just opened…")
    }
    else
    {
        msgbox("That odd tree disappeared without\n"
               "a trace.\p"
               "Oh!\n"
               "That tree was really a POKéMON?")
    }
	release
}

# TODO: Clean up after inline goto
script Route36_EventScript_RockSmashGuy {
	lock
	faceplayer
	if (!flag(FLAG_RECEIVED_HM06))
	{
		if (!flag(FLAG_DEFEATED_SUDOWOODO))
		{
			msgbox("Wa-hey!\p"
				   "I was going to snap that tree with\n"
				   "my straight-arm punch.\p"
				   "But I couldn't!\n"
				   "I'm a failure!")
			release
			end
		}
		else
		{
			msgbox("Did you clear that wretched tree?\n"
			       "I'm impressed!\l"
				   "I want you to have this.")
			giveitem(ITEM_HM06)
			setflag(FLAG_RECEIVED_HM06)
		}
	}
	msgbox("That HM happens to be ROCK SMASH.\n"
		   "You can shatter rocks with just a\l"
		   "single well-aimed smack.\p"
		   "If any rocks are in your way, just\n"
		   "smash 'em up!")
	release
}

script Route36_EventScript_Sudowoodo {
	lock
	waitse
	playse(SE_USSOKI)
	applymovement(SUDOWOODO, Route36_Movement_Sudowoodo)
	waitmovement(0)
	release
}

movement Route36_Movement_Sudowoodo {
	face_right
	delay_8
	face_down
	delay_8
	face_left
	delay_8
	face_down
	delay_8
	face_right
	delay_8
	face_down
	delay_8
	face_left
	delay_8
	face_down
	step_end
}

script Route36_EventScript_DirectionsSign {
    msgbox("ROUTE 36", MSGBOX_SIGN)
}

script Route36_EventScript_RuinsOfAlphSign {
    msgbox("RUINS OF ALPH\n"
		   "NORTH ENTRANCE", MSGBOX_SIGN)
}

raw `
Route36_EventScript_Jerome:: @ 81EE68A
	trainerbattle_single TRAINER_JEROME, Route36_Text_JeromeIntro, Route36_Text_JeromeDefeated
	msgbox Route36_Text_JeromePostBattle, MSGBOX_AUTOCLOSE
	end

Route36_EventScript_Matthew:: @ 81EE6A1
	trainerbattle_single TRAINER_MATTHEW, Route36_Text_MatthewIntro, Route36_Text_MatthewDefeated
	msgbox Route36_Text_MatthewPostBattle, MSGBOX_AUTOCLOSE
	end

Route36_EventScript_Tara:: @ 81EE6B8
	trainerbattle_single TRAINER_TARA, Route36_Text_TaraIntro, Route36_Text_TaraDefeated
	msgbox Route36_Text_TaraPostBattle, MSGBOX_AUTOCLOSE
	end

Route36_EventScript_Missy:: @ 81EE6CF
	trainerbattle_single TRAINER_MISSY, Route36_Text_MissyIntro, Route36_Text_MissyDefeated
	msgbox Route36_Text_MissyPostBattle, MSGBOX_AUTOCLOSE
	end

Route36_EventScript_Carolina:: @ 81EE6E6
	trainerbattle_single TRAINER_CAROLINA, Route36_Text_CarolinaIntro, Route36_Text_CarolinaDefeated
	msgbox Route36_Text_CarolinaPostBattle, MSGBOX_AUTOCLOSE
	end

Route36_EventScript_Cory:: @ 81EE6FD
	trainerbattle_single TRAINER_CORY_1, Route36_Text_CoryIntro, Route36_Text_CoryDefeated, Route36_EventScript_CoryRegisterMatchCallAfterBattle
	specialvar VAR_RESULT, ShouldTryRematchBattle
	compare VAR_RESULT, TRUE
	goto_if_eq Route36_EventScript_CoryRematch
	msgbox Route36_Text_CoryPostBattle, MSGBOX_DEFAULT
	release
	end

Route36_EventScript_CoryRegisterMatchCallAfterBattle:: @ 81EE729
	special PlayerFaceTrainerAfterBattle
	waitmovement 0
	msgbox Route36_Text_CoryRegister, MSGBOX_DEFAULT
	register_matchcall TRAINER_CORY_1
	release
	end

Route36_EventScript_CoryRematch:: @ 81EE748
	trainerbattle_rematch TRAINER_CORY_1, Route36_Text_CoryRematchIntro, Route36_Text_CoryRematchDefeated
	msgbox Route36_Text_CoryRematchPostBattle, MSGBOX_AUTOCLOSE
	end
`