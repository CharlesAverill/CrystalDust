const ELMS_AIDE = 5

mapscripts VioletCity_PokemonCenter_1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setrespawn(HEAL_LOCATION_VIOLET_CITY)
    }
    MAP_SCRIPT_ON_RESUME: CableClub_OnResume
}

script VioletCity_PokemonCenter_1F_EventScript_Nurse {
    setvar(VAR_0x800B, 1)
	call(Common_EventScript_PkmnCenterNurse)
	waitmessage
	waitbuttonpress
	release
}

script VioletCity_PokemonCenter_1F_EventScript_Gameboy_Kid {
    msgbox("A guy named BILL made the POKéMON\n"
           "PC storage system.", MSGBOX_NPC)
}

script VioletCity_PokemonCenter_1F_EventScript_Gentleman {
    msgbox("Around three years ago, TEAM ROCKET\n"
           "was up to no good with POKéMON.\p"
           "But justice prevailed--a young kid\n"
           "broke 'em up.", MSGBOX_NPC)
}

script VioletCity_PokemonCenter_1F_EventScript_Youngster {
    msgbox("POKéMON are smart. They won't obey\n"
           "a TRAINER they don't respect.\p"
           "Without the right GYM BADGES,\n"
           "they'll do just as they please.", MSGBOX_NPC)
}

script VioletCity_PokemonCenter_1F_EventScript_Aide {
    lock
    faceplayer

    if (!flag(FLAG_REFUSED_ELMS_EGG))
    {
        msgbox("{PLAYER}!\n"
               "Long time, no see.\p"
               "PROF. ELM asked me to find you.\n"
               "He has another favor to ask.\p"
               "Would you take the POKéMON EGG?", MSGBOX_YESNO)
    }
    else
    {
        msgbox("{PLAYER}, will you take the EGG?", MSGBOX_YESNO)
    }

    if (var(VAR_RESULT) == YES)
    {
        getpartysize
        if (var(VAR_RESULT) != 6)
        {
            delay(5)
	        textcolor(MSG_COLOR_MISC)
            setflag(FLAG_GOT_ELMS_EGG)
	        playfanfare(MUS_FANFA4)
            message("{PLAYER} received the EGG.")
            waitfanfare
            giveegg(SPECIES_TOGEPI)
            call(RestoreOriginalTextColor)
            msgbox("We discovered that a POKéMON won't\n"
                   "hatch until it grows in the EGG.\p"
                   "It also has to be with other active\n"
                   "POKéMON to hatch.\p"
                   "{PLAYER}, you're the only person we can\n"
                   "rely on.\p"
                   "Please call PROF. ELM when that\n"
                   "EGG hatches!")
            closemessage

            if (var(VAR_FACING) == DIR_NORTH)
            {
                applymovement(EVENT_OBJ_ID_PLAYER, VioletCity_PokemonCenter_1F_Movement_PlayerWatchesAide)
                applymovement(ELMS_AIDE, VioletCity_PokemonCenter_1F_Movement_AideAroundPlayer)
                waitmovement(0)
            }
            else
            {
                applymovement(EVENT_OBJ_ID_PLAYER, Common_Movement_WalkInPlaceFastestDown)
                applymovement(ELMS_AIDE, VioletCity_PokemonCenter_1F_Movement_AideStraightOut)
                waitmovement(0)
            }

	        playse(SE_KAIDAN)
            removeobject(ELMS_AIDE)
            clearflag(FLAG_HIDE_NEW_BARK_TOWN_ELMS_LAB_AIDE)
        }
        else
        {
            msgbox("Oh, no. You can't carry any more\n"
                   "POKéMON with you.\p"
                   "I'll wait here while you make\n"
                   "room for the EGG.")
        }
    }
    else
    {
        msgbox("B-but…\n"
               "PROF. ELM asked for you…")
        setflag(FLAG_REFUSED_ELMS_EGG)
    }

    release
}

movement VioletCity_PokemonCenter_1F_Movement_AideStraightOut {
    walk_down * 1
    walk_right
    walk_down * 3
    delay_4
}

movement VioletCity_PokemonCenter_1F_Movement_AideAroundPlayer {
    walk_right
    walk_down * 4
    delay_4
}

movement VioletCity_PokemonCenter_1F_Movement_PlayerWatchesAide {
    delay_16
    walk_in_place_fastest_right
    delay_16
    walk_in_place_fastest_down
}

raw `
VioletCity_PokemonCenter_1F_EventScript_207A0A:: @ 8207A0A
	msgbox VioletCity_PokemonCenter_1F_Text_207A76, MSGBOX_NPC
	end

VioletCity_PokemonCenter_1F_EventScript_207A13:: @ 8207A13
	msgbox VioletCity_PokemonCenter_1F_Text_207AD6, MSGBOX_NPC
	end

VioletCity_PokemonCenter_1F_EventScript_207A1C:: @ 8207A1C
	lock
	faceplayer
	msgbox VioletCity_PokemonCenter_1F_Text_207B09, MSGBOX_DEFAULT
	specialvar VAR_RESULT, IsStarterInParty
	compare VAR_RESULT, 1
	goto_if_eq VioletCity_PokemonCenter_1F_EventScript_207A38
	release
	end

VioletCity_PokemonCenter_1F_EventScript_207A38:: @ 8207A38
	compare VAR_STARTER_MON, 0
	call_if_eq VioletCity_PokemonCenter_1F_EventScript_207A5B
	compare VAR_STARTER_MON, 1
	call_if_eq VioletCity_PokemonCenter_1F_EventScript_207A64
	compare VAR_STARTER_MON, 2
	call_if_eq VioletCity_PokemonCenter_1F_EventScript_207A6D
	release
	end

VioletCity_PokemonCenter_1F_EventScript_207A5B:: @ 8207A5B
	msgbox VioletCity_PokemonCenter_1F_Text_207BB0, MSGBOX_DEFAULT
	return

VioletCity_PokemonCenter_1F_EventScript_207A64:: @ 8207A64
	msgbox VioletCity_PokemonCenter_1F_Text_207C35, MSGBOX_DEFAULT
	return

VioletCity_PokemonCenter_1F_EventScript_207A6D:: @ 8207A6D
	msgbox VioletCity_PokemonCenter_1F_Text_207CB7, MSGBOX_DEFAULT
	return

VioletCity_PokemonCenter_1F_Text_207A76: @ 8207A76
	.string "That PC-based POKéMON Storage\n"
	.string "System…\p"
	.string "Whoever made it must be some kind\n"
	.string "of a scientific wizard!$"

VioletCity_PokemonCenter_1F_Text_207AD6: @ 8207AD6
	.string "When my POKéMON ate an\n"
	.string "ORAN BERRY, it regained HP!$"

VioletCity_PokemonCenter_1F_Text_207B09: @ 8207B09
	.string "There are many types of POKéMON.\p"
	.string "All types have their strengths and\n"
	.string "weaknesses against other types.\p"
	.string "Depending on the types of POKéMON,\n"
	.string "a battle could be easy or hard.$"

VioletCity_PokemonCenter_1F_Text_207BB0: @ 8207BB0
	.string "For example, your TREECKO\n"
	.string "is a GRASS type.\p"
	.string "It's strong against the WATER and\n"
	.string "GROUND types.\p"
	.string "But, it's weak against FIRE-type\n"
	.string "POKéMON.$"

VioletCity_PokemonCenter_1F_Text_207C35: @ 8207C35
	.string "For example, your TORCHIC\n"
	.string "is a FIRE type.\p"
	.string "It's strong against the GRASS and\n"
	.string "BUG types.\p"
	.string "But, it's weak against WATER-type\n"
	.string "POKéMON.$"

VioletCity_PokemonCenter_1F_Text_207CB7: @ 8207CB7
	.string "For example, your MUDKIP\n"
	.string "is a WATER type.\p"
	.string "It's strong against the FIRE type.\p"
	.string "But, it's weak against GRASS-type\n"
	.string "and ELECTRIC-type POKéMON.$"
`