const KURT = 1

mapscripts AzaleaTown_KurtsHouse_MapScripts {
    
}

script AzaleaTown_KurtsHouse_EventScript_Kurt {
    lock
    faceplayer
    if (flag(FLAG_KURT_GAVE_LURE_BALL))
    {
        // TODO: Apricorn stuff
        msgbox("Yay!")
    }
    elif (flag(FLAG_CLEARED_SLOWPOKE_WELL))
    {
        msgbox("KURT: Hi, {PLAYER}\n"
               "You handled yourself like a real\l"
               "hero at the WELL.\p"
               "I like your style!\n"
               "I would be honored to make BALLS\l"
               "for a TRAINER like you.\p"
               "This is all I have now, but please\n"
               "take it.")
        giveitem(0) // TODO: Lure Ball
        if (!var(VAR_RESULT))
        {
			goto(Common_EventScript_ShowBagIsFull)
        }
        else
        {
            setflag(FLAG_KURT_GAVE_LURE_BALL)
            // TODO: Apricorn stuff
        }
    }
    else
    {
        msgbox("Hm?\n"
               "Who are you?\p"
               "{PLAYER}, eh?\p"
               "You want me to make you some BALLS?\n"
               "Sorry, but that'll have to wait.\p"
               "Do you know who TEAM ROCKET is?\p"
               "Ah, don't worry.\n"
               "I'll tell you anyhow.\p"
               "TEAM ROCKET's an evil gang that\n"
               "uses POKéMON for their dirty work.\p"
               "They're supposed to have disbanded\n"
               "three years ago.\p"
               "Anyway, they're at the WELL, cutting\n"
               "off SLOWPOKETAILS for sale!\p"
               "So I'm going to go give them a\n"
               "lesson in pain!\p"
               "Hang on, SLOWPOKE!\n"
               "Old KURT is on his way!")
        closemessage
        setflag(FLAG_HIDE_AZALEA_TOWN_SLOWPOKE_WELL_GUARD)
        playse(SE_NIGERU)
    
        if (var(VAR_FACING) == DIR_NORTH)
        {
            applymovement(OBJ_EVENT_ID_PLAYER, AzaleaTown_KurtsHouse_Movement_PlayerWatchesKurt)
            applymovement(KURT, AzaleaTown_KurtsHouse_Movement_KurtAroundPlayer)
            waitmovement(0)
        }
        else
        {
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFastestDown)
            applymovement(KURT, AzaleaTown_KurtsHouse_Movement_KurtStraightOut)
            waitmovement(0)
        }
        
        playse(SE_KAIDAN)
        removeobject(KURT)
        waitse
    }
    release
}

movement AzaleaTown_KurtsHouse_Movement_PlayerWatchesKurt {
    delay_8
    walk_in_place_fastest_right
    delay_8
    walk_in_place_fastest_down
}

movement AzaleaTown_KurtsHouse_Movement_KurtAroundPlayer {
    walk_fast_right
    walk_fast_down * 2
    walk_fast_left
    walk_fast_down * 3
}

movement AzaleaTown_KurtsHouse_Movement_KurtStraightOut {
    walk_fast_down * 5
}

script AzaleaTown_KurtsHouse_EventScript_KurtsGranddaughter {
    lock
    faceplayer
    if (flag(FLAG_CLEARED_SLOWPOKE_WELL))
    {
        msgbox("The SLOWPOKE my dad gave me came\n"
               "back! Its TAIL is growing back too!")
    }
    elif (flag(FLAG_HIDE_AZALEA_TOWN_SLOWPOKE_WELL_GUARD))
    {
        msgbox("Grandpa's gone…\n"
               "I'm so lonely…")
    }
    else
    {
        msgbox("The SLOWPOKE are gone…\n"
               "Were they taken away by bad people?")
    }
    release
}

script AzaleaTown_KurtsHouse_EventScript_OakPhoto {
    msgbox("…A young PROF. OAK?", MSGBOX_SIGN)
}

script AzaleaTown_KurtsHouse_EventScript_CelebiStatue {
    msgbox("It's a statue of the forest's\n"
           "protector.", MSGBOX_SIGN)
}

script AzaleaTown_KurtsHouse_EventScript_Slowpoke {
    lock
    faceplayer
    waitse
    playmoncry(SPECIES_SLOWPOKE, 0)
    msgbox("SLOWPOKE: …\n"
           "Yawn?")
    waitmoncry
    release
}
